#+html_head: <link rel="stylesheet" href="https://xieyuheng.github.io/asset/css/page.css" type="text/css" media="screen" />
#+property: tangle core.jo
#+title: core

* [todo-stack]

*** move tests out of core

*** something like 'with-output-to-string' & 'with-output-to-file'

*** module

***** move socket to module

***** bind nanomsg

***** check jo-bound? for module

*** plan

***** minikanren

***** use jojo to write tangle

***** publish sexp as html to file

***** compile sexp to c

* flag

  #+begin_src jojo
  core-flag-on
  test-flag-on
  repl-flag-off
  #+end_src

* <int>

*** even & odd

    #+begin_src jojo
    (+jojo even? 2 mod 0 eq?)
    (+jojo odd? even? not)
    #+end_src

* <jo>

*** jo-append

    #+begin_src jojo
    (+jojo jo-append (-> :jo1 :jo2 -- <jo>)
      :jo1 jo->string
      :jo2 jo->string
      string-append
      string->jo)
    #+end_src

* array

*** array-eqv?

    #+begin_src jojo
    (+jojo array-eqv? (-> :x :y :pred -- <bool>)
      (if :x array-length
          :y array-length
          eq? not then false end)
      :x :y :pred 0 array-eqv?/loop)

    (+jojo array-eqv?/loop (-> :x :y :pred :index -- <bool>)
      (if :index :x array-length gteq? then true end)
      (if :x :index array-ref
          :y :index array-ref
          :pred apply not then false end)
      :x :y :pred :index inc recur)
    #+end_src

* list

*** <null> & <cons>

    #+begin_src jojo
    (+data <null>)
    (+data <cons> .car .cdr)

    (+jojo cons
      (if dup null? then <cons> new end)
      (if dup cons? then <cons> new end)
      "- cons fail" string-write newline
      "  cdr of cons must be <null> or <cons>" string-write newline
      debug)
    #+end_src

*** list?

    #+begin_src jojo
    (+jojo list? (-> :x -- <bool>)
      (case :x
        <null> true
        <cons> true
        else false))
    #+end_src

*** array->list

    #+begin_src jojo
    (+jojo array->list 0 right-of-array->list)

    (+jojo right-of-array->list (-> :array :index -- list)
      (cond [:index :array array-length gteq?] [null]
            else [:array :index inc recur
                  :array :index array-ref
                  swap cons]))
    #+end_src

*** list-spread

    #+begin_src jojo
    (+jojo list-spread (-> :list --)
      (case :list
        <null> []
        <cons> [:list .car :list .cdr recur]))
    #+end_src

*** list-length

    #+begin_src jojo
    (+jojo list-length (-> :list -> <int>)
      (case :list
        <null> 0
        <cons> [:list .cdr recur inc]))
    #+end_src

*** list-any?

    #+begin_src jojo
    (+jojo list-any? (-> :list :pred -> <bool>)
      (cond [:list null?] [false]
            [:list .car :pred apply] [true]
            else [:list .cdr :pred recur]))
    #+end_src

*** list-every?

    #+begin_src jojo
    (+jojo list-every? (-> :list :pred -> <bool>)
      (cond [:list null?] [true]
            [:list .car :pred apply not] [false]
            else [:list .cdr :pred recur]))
    #+end_src

*** list-append

    #+begin_src jojo
    (+jojo list-append (-> :left :right -> list)
      (case :left
        <null> [:right]
        <cons> [:left .cdr :right recur
                :left .car swap cons]))
    #+end_src

*** tail-cons

    #+begin_src jojo
    (+jojo tail-cons null cons list-append)
    #+end_src

*** list-left

    #+begin_src jojo
    (+jojo list-left (-> :list :pred -- left)
      (cond [:list null?] [null]
            [:list .car :pred apply] [null]
            else [:list .car
                  :list .cdr :pred recur
                  cons]))
    #+end_src

*** list-right

    #+begin_src jojo
    (+jojo list-right (-> :list :pred -- right)
      (cond [:list null?] [null]
            [:list .car :pred apply] [:list]
            else [:list .cdr :pred recur]))
    #+end_src

*** list-split

    #+begin_src jojo
    (+jojo list-split (-> :list :pred -- left right)
      (cond [:list null?] [null null]
            [:list .car :pred apply] [null :list]
            else [:list .car
                  :list .cdr :pred recur
                  :right! cons :right]))
    #+end_src

*** list-map

    #+begin_src jojo
    (+jojo list-map (-> :list :fun -- list)
      (case :list
        <null> null
        <cons> [:list .car :fun apply :list .cdr :fun recur cons]))
    #+end_src

*** list-for-each

    #+begin_src jojo
    (+jojo list-for-each (-> :list :fun --)
      (case :list
        <null> []
        <cons> [:list .car :fun apply :list .cdr :fun recur]))
    #+end_src

*** list-filter

    #+begin_src jojo
    (+jojo list-filter (-> :list :pred -- list)
      (cond [:list null?] [null]
            [:list .car :pred apply] [:list .car :list .cdr :pred recur cons]
            else [:list .cdr :pred recur]))
    #+end_src

*** list-reverse

    #+begin_src jojo
    (+jojo list-reverse null swap list-reverse-swap-append)

    (+jojo list-reverse-swap-append (-> left :list -- list)
      (case :list
        <null> []
        <cons> [:list .car swap cons :list .cdr recur]))
    #+end_src

*** list-eqv?

    #+begin_src jojo
    (+jojo list-eqv? (-> :l1 :l2 :eqv -- <bool>)
      (cond [:l1 null? :l2 null? and] [true]
            [:l1 null?] [false]
            [:l2 null?] [false]
            [:l1 .car :l2 .car :eqv apply not] [false]
            else [:l1 .cdr :l2 .cdr :eqv recur]))
    #+end_src

* sexp

*** [note] sexp

    #+begin_src jojo
    (note sexp
      = sexp list -- <null> or <cons>
      | <string>
      | <jo>)
    #+end_src

*** read-sexp

    #+begin_src jojo
    (+jojo read-sexp (-> -- sexp)
      read-jo :jo!
      (cond
        [:jo round-bar eq?]
        [round-ket read-sexp-list-until-ket]

        [:jo square-bar eq?]
        ['begin square-ket read-sexp-list-until-ket cons]

        [:jo flower-bar eq?]
        ['clo flower-ket read-sexp-list-until-ket cons]

        [:jo doublequote eq?]
        [read-string]

        [:jo singlequote eq?]
        ['quote null cons recur tail-cons]

        [:jo backquote eq?]
        ['partquote null cons recur tail-cons]

        else :jo))

    (+jojo read-sexp-list-until-ket (-> :ket <jo> -- sexp list)
      read-jo :jo!
      (if :jo :ket eq?
          then null
          else :jo jo-unread read-sexp, :ket recur, cons))
    #+end_src

*** sexp-write

    #+begin_src jojo
    (+jojo sexp-write (-> :sexp --)
      (case :sexp
        <null> ['null jo-write]
        <cons> [round-bar jo-write :sexp sexp-list-write
                round-ket jo-write]
        <string> [doublequote jo-write :sexp string-write
                  doublequote jo-write]
        <jo> [:sexp jo-write]))

    (+jojo sexp-list-write (-> :list sexp list --)
      (cond
        [:list null?] []
        [:list .cdr null?] [:list .car sexp-write]
        else [:list .car sexp-write space
              :list .cdr recur]))
    #+end_src

* repl

*** repl

    #+begin_src jojo
    (+jojo repl (-> <input-stack> --)
      reading-stack-push
      repl/loop
      reading-stack-pop input-stack-free)

    (+jojo repl/loop
      (if has-jo? not then end)
      read-sexp sexp-eval
      (if repl-flag then print-data-stack)
      recur)

    (+jojo repl-over-repl-in-c
      repl/loop)
    #+end_src

*** sexp-eval

    #+begin_src jojo
    (+jojo sexp-eval (-> :sexp --)
      (case :sexp
        <null>   [:sexp sexp-compile-jojo apply]
        <cons>   [:sexp sexp-compile-jojo apply]
        <string> [:sexp sexp-compile-jojo apply]
        <jo>     [:sexp jo-eval]))
    #+end_src

*** jo-eval

    #+begin_src jojo
    (+jojo jo-eval (-> :jo --)
      (cond
        [:jo int-jo?] [:jo sexp-compile-jojo apply]

        [:jo local-jo?
         :jo set-local-jo? or
         :jo dynamic-local-jo? or
         :jo set-dynamic-local-jo? or
         :jo field-jo? or
         :jo set-field-jo? or]
        ["- jo-eval can not handle : " string-write
         :jo jo-write newline]

        [:jo comma eq?] []

        [:jo jo-bound? not]
        ["- jo is not bound : " string-write
         :jo jo-write newline]

        else [:jo sexp-compile-jojo apply]))
    #+end_src

* compiler

*** sexp-list-compile-jojo

    #+begin_src jojo
    (+jojo sexp-list-compile-jojo (-> :body -- <jojo>)
      compiling-stack-tos :address!
      :body sexp-list-compile
       emit-jojo-end
      :address new-jojo
      compiling-stack-drop :address compiling-stack-push)
    #+end_src

*** sexp-compile-jojo

    #+begin_src jojo
    (+jojo sexp-compile-jojo null cons sexp-list-compile-jojo)
    #+end_src

*** sexp-list-compile

    #+begin_src jojo
    (+jojo sexp-list-compile (-> :list sexp list --)
      (case :list
        <null> []
        <cons> [:list .car sexp-compile
                :list .cdr recur]))
    #+end_src

*** sexp-compile

    #+begin_src jojo
    (+jojo sexp-compile (-> :sexp --)
      (case :sexp
        <null>   [:sexp emit-lit]
        <cons>   [:sexp .cdr :sexp .car jo-apply]
        <string> [:sexp emit-lit]
        <jo>     [:sexp jo-compile]))
    #+end_src

*** jo-compile

    #+begin_src jojo
    (+jojo jo-compile (-> :jo --)
      (cond
        [:jo int-jo?] [:jo jo->int emit-lit]

        [:jo local-jo?]     [:jo jo-emit-local]
        [:jo set-local-jo?] [:jo jo-emit-set-local]

        [:jo dynamic-local-jo?]     [:jo jo-emit-dynamic-local]
        [:jo set-dynamic-local-jo?] [:jo jo-emit-set-dynamic-local]

        [:jo field-jo?]     [:jo jo-emit-field]
        [:jo set-field-jo?] [:jo jo-emit-set-field]

        [:jo comma eq?] []

        else [:jo jo-emit]))
    #+end_src

*** sexp-list-run

    #+begin_src jojo
    (+jojo sexp-list-run sexp-list-compile-jojo apply)
    #+end_src

*** sexp-run

    #+begin_src jojo
    (+jojo sexp-run sexp-compile-jojo apply)
    #+end_src

* basic syntax

*** (quote)

    #+begin_src jojo
    (+jojo quote {emit-lit} list-for-each)

    (note
      (+jojo quote {sexp-quote-compile} list-for-each)

      (+jojo sexp-quote-compile (-> :sexp --)
        (if :sexp cons?
            then :sexp sexp-list-quote-compile
            else :sexp emit-lit))

      (+jojo sexp-list-quote-compile (-> :list --)
        (cond
          [:list null?]
          [null emit-lit]

          [:list .car cons? not]
          [:list .cdr recur
           :list .car emit-lit
           'swap jo-emit
           'cons jo-emit]

          else
          [:list .cdr recur
           :list .car recur
           'swap jo-emit
           'cons jo-emit])))
    #+end_src

*** (partquote)

    #+begin_src jojo
    (+jojo partquote {sexp-partquote-compile} list-for-each)

    (+jojo sexp-partquote-compile (-> :sexp --)
      (if :sexp cons?
          then :sexp sexp-list-partquote-compile
          else :sexp emit-lit))

    (note this function depends on (list))

    (+jojo sexp-list-partquote-compile (-> :list --)
      (cond
        [:list null?]
        [null emit-lit]

        [:list .car cons? not]
        [:list .cdr recur
         :list .car emit-lit
         'swap jo-emit
         'cons jo-emit]

        [:list .car .car '@ eq?]
        [:list .cdr recur
         :list .car .cdr list
         'swap jo-emit
         'list-append jo-emit]

        else
        [:list .cdr recur
         :list .car recur
         'swap jo-emit
         'cons jo-emit]))
    #+end_src

*** (if)

    #+begin_src jojo
    (+jojo if-else-then (-> :body --)
      :body {'then eq?} list-split (-> :question :then-else)
      :then-else {'else eq?} list-split (-> :then :else)
      :question sexp-list-compile
      emit-jz :address-for-jz!
      :then .cdr sexp-list-compile
      emit-jmp :address-for-jmp!
      :address-for-jz set-offset-to-here
      :else .cdr sexp-list-compile
      :address-for-jmp set-offset-to-here)

    (+jojo if-then (-> :body --)
      :body {'then eq?} list-split (-> :question :then)
      :question sexp-list-compile
      emit-jz :address-for-jz!
      :then .cdr sexp-list-compile
      :address-for-jz set-offset-to-here)

    (+jojo core-if (-> :body --)
      (cond
        [:body {'else eq?} list-any?
         :body {'then eq?} list-any? and]
        [:body if-else-then]

        [:body {'then eq?} list-any?]
        [:body if-then]

        else ["- (if) fail" string-write newline
              "  the body dose not has 'then" string-write newline
              "  body : " string-write :body sexp-list-write newline
              debug]))
    #+end_src

*** (clo)

    #+begin_src jojo
    (+jojo core-clo
      sexp-list-compile-jojo emit-lit
      'current-local-env jo-emit
      'closure jo-emit)
    #+end_src

*** (cond)

    #+begin_src jojo
    (+jojo cond/expend (-> :body -- sexp)
      (cond
        [:body list-length 2 eq?]
        [:body .car :body .cdr .car cond/expend-if-then
         'else tail-cons
         'cond/miss-match-report tail-cons
         'debug tail-cons]

        else
        [:body .car :body .cdr .car cond/expend-if-then
         'else tail-cons
         :body .cdr .cdr recur tail-cons]))

    (+jojo cond/miss-match-report
      "- runtime (cond) miss match" string-write newline)

    (note
      (+jojo cond/expend-if-then (-> :question :answer -- sexp)
        `(if (@ (if :question 'else eq?
                    then 'true
                    else :question))
             then (@ :answer))))

    (+jojo cond/expend-if-then (-> :question :answer -- sexp)
      'if null cons
      (if :question 'else eq?
          then 'true
          else :question)
      tail-cons
      'then tail-cons
      :answer tail-cons)
    #+end_src

*** (->)

    #+begin_src jojo
    (+jojo arrow/expend (-> :body -- sexp)
      :body {'-- eq?} list-left
      {local-jo?} list-filter
      {local-jo->set-local-jo} list-map
      list-reverse
      'begin swap cons)
    #+end_src

*** jo-list-compile-cells

    #+begin_src jojo
    (+jojo jo-list-compile-cells (-> :list -- <address>)
      compiling-stack-tos :address!
      :list {jo-emit} list-for-each
      emit-zero
      :address cells-copy :new-address!
      compiling-stack-drop :address compiling-stack-push
      :new-address)
    #+end_src

*** (+data)

    #+begin_src jojo
    (+jojo plus-data (-> :body --)
      (if :body .car tag-jo? not then
          "- (+data) fail" string-write newline
          "  name must be of form <...>" string-write newline
          "  body : " string-write :body sexp-list-write newline
          end)

      :body .cdr
      {field-jo?} list-filter
      jo-list-compile-cells :address!

      :address :body .car name-bind-data)
    #+end_src

*** (+gene)

    #+begin_src jojo
    (+jojo plus-gene (-> :body --)
      :body .car :name!
      :body .cdr :rest-body!
      :body .cdr .car :arrow-sexp!

      :arrow-sexp .cdr
      {'-- eq?} list-left
      {local-jo?} list-filter
      list-length
      :name name-bind-gene

      :rest-body sexp-list-compile-jojo
      :name name-bind-disp-default-to-jojo)
    #+end_src

*** (+disp)

    - this syntax always use <jojo> as disp

    #+begin_src jojo
    (+jojo plus-disp (-> :body --)
      :body .car :name!
      :body .cdr :rest-body!
      :body .cdr .car :arrow-sexp!

      :arrow-sexp .cdr
      {'-- eq?} list-left
      {tag-jo?} list-filter
      jo-list-compile-cells :address-of-tags!

      :rest-body sexp-list-compile-jojo
      :address-of-tags
      :name name-bind-disp-to-jojo)
    #+end_src

* reboot basic syntax -- the order matters

*** re-define (+jojo)

    #+begin_src jojo
    (+jojo +jojo (-> :body sexp list --)
      :body .cdr sexp-list-compile-jojo
      :body .car name-bind)
    #+end_src

*** run repl-over-repl-in-c

    #+begin_src jojo
    repl-over-repl-in-c
    #+end_src

*** re-define new keywords

    #+begin_src jojo
    (+jojo note drop)

    (+jojo begin sexp-list-compile)

    (+jojo if core-if)
    (+jojo clo core-clo)

    (+jojo cond cond/expend sexp-compile)
    (+jojo -> arrow/expend sexp-compile)

    (+jojo +data plus-data)
    (+jojo +gene plus-gene)
    (+jojo +disp plus-disp)
    #+end_src

* more syntax

*** (array)

    #+begin_src jojo
    (+jojo array array/expend sexp-compile)

    (+jojo array/expend (-> :body -- sexp)
      'begin null cons
      'mark tail-cons
      :body list-append
      'collect tail-cons)
    #+end_src

*** (list)

    #+begin_src jojo
    (+jojo list list/expend sexp-compile)

    (+jojo list/expend (-> :body -- sexp)
      'begin null cons
      'mark tail-cons
      :body list-append
      'collect tail-cons
      'array->list tail-cons)
    #+end_src

*** (assert) & (assert!)

    #+begin_src jojo
    (+jojo assert assert/expend sexp-compile)

    (+jojo assert/expend (-> :body -- sexp)
      `(if (@ :body list-spread)
           then
           else
           "- (assert) fail" string-write newline
           "  assertion : " string-write
           (quote (@ :body)) sexp-list-write newline))

    (+jojo assert! assert!/expend sexp-compile)

    (+jojo assert!/expend (-> :body -- sexp)
      `(if (@ :body list-spread)
           then
           else
           "- (assert!) fail" string-write newline
           "  assertion : " string-write
           (quote (@ :body)) sexp-list-write newline
           debug))
    #+end_src

*** (test)

    #+begin_src jojo
    (+jojo test (-> :body --)
      (if test-flag then :body begin))
    #+end_src

*** (let-bind) -- moand interface

    #+begin_src jojo
    (note example
      (let-bind bind-maybe
        :l [:t1 .l :t2 .l zip-tree]
        :r [:t1 .r :t2 .r zip-tree]
        [:l :r node return-maybe])
      (begin
        [:t1 .l :t2 .l tree-zip]
        {:l! [:t1 .r :t2 .r zip-tree]
         {:r! [:l :r node return-maybe]}
         bind-maybe}
        bind-maybe)
      (begin
        [:t1 .l :t2 .l tree-zip] {:l!
        [:t1 .r :t2 .r zip-tree] {:r!
        [:l :r node return-maybe]} bind-maybe} bind-maybe))

    (+jojo let-bind let-bind/expend sexp-compile)

    (+jojo let-bind/expend (-> :body -- sexp)
      :body .car :body .cdr let-bind/expend-recur)

    (+jojo let-bind/expend-recur (-> :binder :rest -- sexp)
      (cond
        [:rest list-length 1 eq?]
        [:rest .car]

        [:rest .car local-jo?]
        `[(@ :rest .cdr .car)
          {(@ :rest .car local-jo->set-local-jo
              :binder :rest .cdr .cdr recur)}
          (@ :binder)]

        else
        `[(@ :rest .car)
          {drop
           (@ :binder :rest .cdr recur)}
          (@ :binder)]))
    #+end_src

*** (case)

    #+begin_src jojo
    (+jojo case case/expend sexp-compile)

    (+jojo case/expend (-> :body -- sexp)
      `(begin (list (@ :body .car)) {tag} list-map
         (@ :body .cdr case/expend-rest)))

    (+jojo case/expend-rest (-> :body -- sexp)
      (cond
        [:body list-length 2 eq?]
        [:body .car :body .cdr .car case/expend-if-then
         `(else "- runtime (case) miss match" string-write newline
                debug)
         list-append]

        else
        [:body .car :body .cdr .car case/expend-if-then
         `(else (@ :body .cdr .cdr recur))
         list-append]))

    (+jojo case/expend-if-then (-> :tags :answer -- sexp)
      (cond [:tags 'else eq?]
            `(if true
                 then drop (@ :answer))
            [:tags cons?]
            `(if dup (quote (@ :tags .cdr)) {case/match?} list-eqv?
                 then drop (@ :answer))
            else
            `(if dup .car (quote (@ :tags)) case/match?
                 then drop (@ :answer))))

    (+jojo case/match? (-> :tag1 :tag2 -- <bool>)
      (cond [:tag1 underscore-jo?] [true]
            [:tag2 underscore-jo?] [true]
            else [:tag1 :tag2 eq?]))
    #+end_src

* [test] syntax

*** (list)

    #+begin_src jojo
    (test

      (assert
        mark 0 1 2 3 4 collect
        array->list
        (list 0 1 2 3 4)
        {eq?} list-eqv?)

      (assert
        (list 0 1 2 3 4)
        (list 5 6 7 8 9)
        list-append
        (list 0 1 2 3 4 5 6 7 8 9)
        {eq?} list-eqv?)

      (assert
        (list 0 1 2 3 4 5 6 7 8 9)
        {5 gteq?} list-left
        (list 0 1 2 3 4)
        {eq?} list-eqv?)

      (assert
        (list 0 1 2 3 4 5 6 7 8 9)
        {5 gteq?} list-split
        swap (list 0 1 2 3 4) {eq?} list-eqv?
        swap (list 5 6 7 8 9) {eq?} list-eqv?
        and)

      (assert
        (list 0 1 2 3 4 5 6 7 8 9)
        {inc} list-map
        (list 1 2 3 4 5 6 7 8 9 10)
        {eq?} list-eqv?)

      (assert
        (list 0 1 2 3 4 5 6 7 8 9)
        {2 mod 0 eq?} list-filter
        (list 0 2 4 6 8)
        {eq?} list-eqv?)

      (assert
        (list 0 1 2 3 4 5 6 7 8 9)
        list-reverse
        (list 9 8 7 6 5 4 3 2 1 0)
        {eq?} list-eqv?))
    #+end_src

*** (+jojo)

    #+begin_src jojo
    (test
      (+jojo square dup mul)
      (assert 2 square 4 eq?))
    #+end_src

*** (cond)

    #+begin_src jojo
    (test
      (+jojo list-length/cond
        :list!
        (cond
          [:list null?] [0]
          else [:list .cdr recur inc]))
      (assert (list 1 2 3 4 5) list-length/cond 5 eq?))
    #+end_src

*** (clo)

    #+begin_src jojo
    (test
      (+jojo com (-> :m1 :m2 -- jojo)
        {:m1 apply :m2 apply})
      (assert {1} {2} com apply add 3 eq?)
      (assert {1} {2} com {3} com apply add add 6 eq?)
      (assert {1} {2} {3} com com apply add add 6 eq?))
    #+end_src

*** (+gene) & (+disp)

    #+begin_src jojo
    (test
      (+gene add-two (-> :x :y --) 0)
      (+disp add-two (-> <string> <int> --) swap string-length add)
      (+disp add-two (-> <int> <string> --) string-length add)
      (+disp add-two (-> <int> <int> --) add)
      (+disp add-two (-> <string> <string> --)
        string-length swap string-length add)

      (assert "123" 3 add-two 6 eq?)
      (assert 3 "123" add-two 6 eq?)
      (assert 3 3 add-two 6 eq?)
      (assert "123" "123" add-two 6 eq?)
      (assert '123 "123" add-two 0 eq?))
    #+end_src

*** (partquote)

    #+begin_src jojo
    (test
      (+jojo one-two-three 'one 'two 'three)
      (+jojo one-two-three-list '(one two three))
      (assert
        '(1 2 3)
        '(1 2 3)
        {eq?} list-eqv?)
      (assert
        `(1 2 3 (@ one-two-three) 1 2 3)
        '(1 2 3 one two three 1 2 3)
        {eq?} list-eqv?)
      (assert
        `(1 2 3 (@ one-two-three-list list-spread) 1 2 3)
        '(1 2 3 one two three 1 2 3)
        {eq?} list-eqv?)
      (assert
        `(((@ 1))) .car .car
        1 eq?)
      (assert
        `(((@ `(((@ 1)))))) .car .car .car .car
        1 eq?))
    #+end_src

*** (case)

    #+begin_src jojo
    (test
      (+jojo list-length/case
        :list!
        (case :list
          <null> 0
          <cons> [:list .cdr recur inc]))
      (assert (list 1 2 3 4 5) list-length/case 5 eq?))
    #+end_src

*** dynamic-local

    #+begin_src jojo
    (test
      (+jojo fun-1
        ::dynamic-local)
      (+jojo fun-2
        'dynamic-of-fun-2 ::dynamic-local!
        fun-1
        nop)
      (assert
        'dynamic-of-fun-2 fun-2 eq?)
      (assert
        'dynamic-of-top-begin ::dynamic-local!
        fun-2
        fun-1
        swap 'dynamic-of-fun-2 eq?
        swap 'dynamic-of-top-begin eq?
        and))
    #+end_src

* monad

*** [note] bind and compose can implement each ohter

    #+begin_src jojo
    (note

      (+jojo bind
        (-> (: :1m [:<1> <monad>])
            (: :1-2m (-> :<1> -- :<2> <monad>))
         -- (: :2m [:<2> <monad>]))
        dummy {drop :1m} :1-2m compose apply)

      (+jojo compose
        (-> (: :0-1m (-> :<0> -- :<1> <monad>))
            (: :1-2m (-> :<1> -- :<2> <monad>))
         -- (: :0-2m (-> :<0> -- :<2> <monad>)))
        {:0-1m apply :1-2m bind}))
    #+end_src

*** maybe monad

    #+begin_src jojo
    (+data <nothing>)
    (+data <just> .v)

    (+jojo return-maybe just)

    (+jojo bind-maybe (-> :m :v->m -- maybe)
      (case :m
        <nothing> nothing
        <just> [:m .v :v->m apply]))
    #+end_src

*** >< list monad

    #+begin_src jojo
    (note
      (+jojo return-list)
      (+jojo bind-list))
    #+end_src

* pair

*** <pair>

    #+begin_src jojo
    (+data <pair> .l .r)
    #+end_src

* [test] monad

*** tree

    #+begin_src jojo
    (test

      (+data <leaf> .v)
      (+data <node> .l .r)

      (+jojo tree-1
        1 leaf 2 leaf node
        3 leaf 4 leaf node
        5 leaf node
        node))
    #+end_src

*** tree-map

    #+begin_src jojo
    (test

      (+jojo tree-map (-> :tree :fun -- tree)
        (case :tree
          <leaf> [:tree .v :fun apply leaf]
          <node> [:tree .l :fun recur
                  :tree .r :fun recur node]))

      (+jojo tree-eqv? (-> :t1 :t2 :eqv --)
        (case [:t1 :t2]
          [<leaf> <leaf>] [:t1 .v :t2 .v :eqv apply]
          [<node> <node>] (if :t1 .l :t2 .l :eqv recur not
                              then false
                              else :t1 .r :t2 .r :eqv recur)
          else false))

      (assert
        tree-1 {100 add} tree-map

        101 leaf 102 leaf node
        103 leaf 104 leaf node
        105 leaf node
        node

        {eq?} tree-eqv?))
    #+end_src

*** tree-zip -- maybe monad

    #+begin_src jojo
    (test

      (+jojo tree-zip (-> :t1 :t2 -- tree maybe)
        (case [:t1 :t2]
          [<leaf> <leaf>] [:t1 .v :t2 .v pair leaf return-maybe]
          [<node> <node>] (let-bind bind-maybe
                            :l [:t1 .l :t2 .l tree-zip]
                            :r [:t1 .r :t2 .r tree-zip]
                            [:l :r node return-maybe])
          else nothing))

      (+jojo maybe-write (-> :m :f --)
        (case :m
          <nothing> ['nothing jo-write space]
          <just> [:m .v :f apply 'just jo-write space]))

      (assert
        tree-1 dup tree-zip tag '<just> eq?)

      (+jojo tree-2
        1 1 pair leaf 2 2 pair leaf node
        3 3 pair leaf 4 4 pair leaf node
        5 5 pair leaf node
        node)

      (assert
        tree-1 dup tree-zip .v
        tree-2
        {(-> :pair1 :pair2)
         :pair1 .l :pair2 .l eq?
         :pair1 .r :pair2 .r eq? and}
        tree-eqv?)

      (assert
        tree-1 1 leaf tree-zip nothing?))
    #+end_src

*** number-tree -- intrinsic state monad

    #+begin_src jojo
    (test

      (+jojo number-tree (-> number, :tree -- number, tree)
        (case :tree
          <leaf> [dup inc swap leaf]
          <node> [:tree .l recur :l!
                  :tree .r recur :r!
                  :l :r node]))

      (+jojo tree-3
        "1" leaf "2" leaf node
        "3" leaf "4" leaf node
        "5" leaf node
        node)

      (assert
        0 tree-3 number-tree swap drop
        tree-1 {dec} tree-map
        {eq?} tree-eqv?))
    #+end_src

* combinator

*** times

    #+begin_src jojo
    (+jojo times (-> :fun :n --)
      (if :n 0 lteq? then end)
      :fun apply
      :fun :n dec recur)
    #+end_src

* w -- write -- gene

*** w

    #+begin_src jojo
    (+jojo write w)

    (+gene w (-> :x --) :x data-print)

    (+disp w (-> <int> --) int-write)
    (+disp w (-> <string> --) string-write)
    (+disp w (-> <jo> --) jo-write)
    (+disp w (-> <byte> --) "(byte " w byte-write ")" w)
    #+end_src

*** list-write

    #+begin_src jojo
    (+disp w (-> <null> --) drop "()" w)

    (+disp w (-> <cons> --) list-write)

    (+jojo list-write (-> :list --)
      "(" w :list list-write/loop ")" w)

    (+jojo list-write/loop (-> :list --)
      (cond
        [:list null?] []
        [:list .cdr null?] [:list .car w]
        else [:list .car w space
              :list .cdr recur]))
    #+end_src

*** array-write

    #+begin_src jojo
    (+disp w (-> <array> --) array-write)

    (+jojo array-write (-> :array --)
      (if :array array-length 0 eq? then "(array)" w end)
      "(array " w :array 0 array-write/loop ")" w)

    (+jojo array-write/loop (-> :array :index --)
      (if :index :array array-length 1 sub eq? then
          :array :index array-ref w end)
      :array :index array-ref w space
      :array :index inc recur)
    #+end_src

*** pair-write

    #+begin_src jojo
    (+disp w (-> <pair>) pair-write)

    (+jojo pair-write (-> :pair --)
      :pair .l w space
      :pair .r w space
      "pair" w)
    #+end_src

*** nl

    #+begin_src jojo
    (+jojo nl newline)
    #+end_src

* equal? -- gene

*** equal?

    #+begin_src jojo
    (+gene equal? (-> :x :y -- <bool>) :x :y eq?)

    (+disp equal? (-> <string> <string> -- <bool>) string-eq?)
    (+disp equal? (-> :x <cons> :y <cons> -- <bool>)
      (if :x .car :y .car equal? not
          then false
          else :x .cdr :y .cdr equal?))

    (+disp equal? (-> <array> <array> -- <bool>)
      {equal?} array-eqv?)

    (+disp equal? (-> :x <pair> :y <pair> -- <bool>)
      (if :x .l :y .l equal? not
          then false
          else :x .r :y .r equal?))
    #+end_src

* more list

*** list-foldr

    #+begin_src jojo
    (+jojo list-foldr (-> a :list, :b, :a-b->b -- b)
      (case :list
        <null> :b
        <cons> [:list .car
                :list .cdr :b :a-b->b recur
                :a-b->b apply]))
    #+end_src

*** list-foldl

    #+begin_src jojo
    (+jojo list-foldl (-> a :list, :b, :b-a->b -- b)
      (case :list
        <null> :b
        <cons> [:list .cdr :b :b-a->b recur
                :list .car
                :b-a->b apply]))
    #+end_src

*** list-member?

    #+begin_src jojo
    (+jojo list-member? (-> :list :x -- <bool>)
      :list {:x equal?} list-any?)
    #+end_src

*** [test]

    #+begin_src jojo
    (test

      (assert
        (list 0 1 2 3 4 5) () {pair} list-foldr
        0 1 2 3 4 5 () pair pair pair pair pair pair
        equal?)

      (assert
        (list 0 1 2 3 4 5) () {pair} list-foldl
        () 5 pair 4 pair 3 pair 2 pair 1 pair 0 pair
        equal?)

      (assert
        (list
          (list 1 2 3)
          (list 4 5 6)
          (list 7 8 9))
        () {list-append} list-foldr

        (list 1 2 3, 4 5 6, 7 8 9)
        equal?)

      (assert
        (list
          (list 1 2 3)
          (list 4 5 6)
          (list 7 8 9))
        () {list-append} list-foldl
        (list 7 8 9, 4 5 6, 1 2 3)
        equal?))
    #+end_src

* table

*** <null-table> & <cons-table>

    #+begin_src jojo
    (+data <null-table>)
    (+data <cons-table> .rest .key .value)
    #+end_src

*** table?

    #+begin_src jojo
    (+jojo table? (-> :x -- <bool>)
      (case :x
        <null-table> true
        <cons-table> true
        else false))
    #+end_src

*** list->table

    #+begin_src jojo
    (+jojo list->table (-> :list -- table)
      (if :list list-length odd? then
          "- list->table fail" w nl
          "  length of list is not even" w nl
          "  length : " w :list list-length w nl
          "  list : " w :list w nl
          debug)
      :list list->table/recur)

    (+jojo list->table/recur (-> :list -- table)
      (case :list
        <null> null-table
        <cons> [:list .cdr .cdr recur
                :list .car :list .cdr .car
                cons-table]))
    #+end_src

*** table

    #+begin_src jojo
    (+jojo table list 'list->table jo-emit)
    #+end_src

*** table-write

    #+begin_src jojo
    (+disp w (-> <null-table>) drop "(table)" w)

    (+disp w (-> <cons-table>) table-write)

    (+jojo table-write (-> :table --)
      (if :table null-table? then "(table)" w end)
      "(table " w :table table-write/loop ")" w)

    (+jojo table-write/loop (-> :table --)
      (cond
        [:table null-table?] []
        [:table .rest null-table?] [:table .key w space :table .value w]
        else [:table .key w space :table .value w comma w space
              :table .rest recur]))
    #+end_src

*** table-find

    #+begin_src jojo
    (+jojo table-find (-> :table :key -- value true or false)
      (cond [:table null-table?] false
            [:table .key :key equal?] [:table .value true]
            else [:table .rest :key recur]))
    #+end_src

*** table-contain?

    #+begin_src jojo
    (+jojo table-contain? (-> :large :small -- <bool>)
      (cond [:small null-table?] true
            [:large :small .key table-find]
            (if :small .value equal?
                then :large :small .rest recur
                else false)
            else false))
    #+end_src

*** table-merge

    #+begin_src jojo
    (+jojo table-merge (-> :base :src -- table true or key false)
      (cond [:src null-table?] [:base true]
            [:base :src .key table-find]
            (if :src .value equal?
                then :base :src .rest recur
                else :src .key false end)
            else [:base :src .key :src .value cons-table
                  :src .rest recur]))
    #+end_src

*** table-equal?

    #+begin_src jojo
    (+jojo table-equal? (-> :t1 :t2 -- <bool>)
      (if :t1 :t2 table-contain? not
          then false end
          else :t2 :t1 table-contain?))
    #+end_src

*** table-key-filter

    #+begin_src jojo
    (+jojo table-key-filter (-> :table :pred -- :table)
      (cond [:table null-table?] null-table
            [:table .key :pred apply]
            [:table .rest :pred recur
             :table .key :table .value cons-table]
            else [:table .rest :pred recur]))
    #+end_src

*** table-key-map

    #+begin_src jojo
    (+jojo table-key-map (-> :table :fun -- :table)
      (cond [:table null-table?] null-table
            else [:table .rest :fun recur
                  :table .key :fun apply
                  :table .value cons-table]))
    #+end_src

*** [test]

    #+begin_src jojo
    (test

      (assert
        (table 1 "1", 2 "2", 3 "3")
        (table 2 "2", 3 "3", 4 "4")
        (assert table-merge)
        (table 1 "1", 2 "2", 3 "3", 4 "4")
        table-equal?)

      (assert
        (table 1 "1", 2 "2", 3 "3", 4 "4")
        {even?} table-key-filter
        (table 2 "2", 4 "4")
        table-equal?)

      (assert
        (table 1 "1", 2 "2", 3 "3", 4 "4")
        {dec} table-key-map
        (table 0 "1", 1 "2", 2 "3", 3 "4")
        table-equal?))
    #+end_src

* byte

*** (byte)

    #+begin_src jojo
    (+jojo byte {jo->byte emit-lit} list-for-each)
    #+end_src

* system

*** cmd-list

    #+begin_src jojo
    (+jojo cmd-list 0 cmd-list/loop)

    (+jojo cmd-list/loop (-> :index --)
      (cond [:index cmd-number eq?] null
            else [:index index->cmd-string
                  :index inc recur
                  cons]))
    #+end_src

*** dash-prefixed-string?

    #+begin_src jojo
    (+jojo dash-prefixed-string? (-> :str -- <bool>)
      (cond [:str string-length 1 lt?] false
            [:str 0 string-ref (byte -) eq?] true
            else false))
    #+end_src

*** usr-jojo-dir

    #+begin_src jojo
    (+jojo usr-jojo-dir
      (cond ["USR_JOJO_DIR" find-env-string] []
            ["HOME" find-env-string] ["/.jojo" string-append]
            else ["- usr-jojo-dir fail" w nl
                  "  system env USR_JOJO_DIR is empty" w nl
                  "  system env HOME is empty" w nl
                  debug]))
    #+end_src

*** sys-jojo-dir

    #+begin_src jojo
    (+jojo sys-jojo-dir
      (cond ["SYS_JOJO_DIR" find-env-string] []
            else "/usr/lib/jojo"))
    #+end_src

* <file>

*** maybe-drop-shabang

    #+begin_src jojo
    (+jojo maybe-drop-shabang
      (-> <input-stack> -- <input-stack>)
      (if dup input-stack-empty? then end)
      reading-stack-push
      read-line :line!
      (cond [:line string-length 3 lteq?] [:line string-unread]
            [:line 0 2 string-slice "#!" string-eq?] []
            else [:line string-unread])
      reading-stack-pop)
    #+end_src

*** input-stack-read-sexp-list

    #+begin_src jojo
    (+jojo input-stack-read-sexp-list
      (-> <input-stack> -- sexp list)
      reading-stack-push
      read-sexp-list-to-the-end
      reading-stack-pop input-stack-free)
    #+end_src

*** read-sexp-list-to-the-end

    #+begin_src jojo
    (+jojo read-sexp-list-to-the-end (-> -- sexp list)
      (if has-jo? not then null end)
      read-sexp recur cons)
    #+end_src

*** absolute-path?

    #+begin_src jojo
    (+jojo absolute-path? (-> :path -- <bool>)
      (if :path string-length 0 eq? then false end)
      :path 0 string-ref (byte /) eq?)
    #+end_src

*** path->reading-path

    #+begin_src jojo
    (+jojo path->reading-path (-> :path -- path)
      (if :path absolute-path?
          then :path
          else current-reading-dir :path string-append))
    #+end_src

*** path-readable?

    #+begin_src jojo
    (+jojo path-readable? (-> :path -- <bool>)
      (if :path path-open-read
          then file-close true
          else drop false))
    #+end_src

*** path-load

    #+begin_src jojo
    (+jojo path-load (-> :path --)
      :path path->reading-path :real-path!
      (cond [:real-path path-open-read]
            [:file!
             :file file-input-stack
             maybe-drop-shabang
             repl
             :file file-close]
            else [:error-number!
                  "- path-load fail" w nl
                  "  path : " w :path w nl
                  "  real-path : " w :real-path w nl
                  "  " w :error-number error-number-print nl
                  debug]))
    #+end_src

* module

*** [note] module system

    - a module in jojo is a global name
      stores a table of exported names,

    - while a module in file system is a dir of files,
      with a module.jo in the dir,
      to store meta data of the module.

    - every module has a version,
      - in jojo, the global name
        should be "<module-name>/<version>"
      - in file system, the dir of a module
        should be "<module-name>/<version>/*"

    - a module will be installed to "~/.jojo"
      as "~/.jojo/modules/<module-name>/<version>/*"

    - module system solves two problems :

      - re-load problem :
        when used by multiple clients,
        a module should only be loaded once.

      - unique-name problem :
        a name must be resolved to an unique-name

*** (+module)

    #+begin_src jojo
    (+jojo +module
      dup .car :name! .cdr :body!

      :body expend-include :body!

      :body :name
      module/generate-define-table
      :define-table!

      :body :name
      module/generate-export-table
      :export-table!

      :define-table
      :export-table
      module/export-check

      :body module/collect-import-table-list
      :import-table-list!

      :import-table-list
      {(if table-merge not then
           "- (+module) fail to merge import-table-list" w nl
           "  import-table-list : " w :import-table-list w nl
           "  key : " w w nl
           "  module name : " w :name w nl
           debug)}
      null-table swap list-foldr :import-table!

      :import-table :define-table
      (if table-merge not then
          "- (+module) fail to merge import-table with define-table" w nl
          "  import-table : " w :import-table w nl
          "  define-table : " w :define-table w nl
          "  key : " w w nl
          "  module name : " w :name w nl
          debug)
      :rename-table!

      :body
      {sexp-for-module? not} list-filter
      {:rename-table sexp-table-substitute} list-map
      sexp-list-compile

      :export-table
      :name name-bind)
    #+end_src

*** expend-include

    #+begin_src jojo
    (+jojo expend-include (-> :body -- body)
      (cond [:body null?] null
            [:body .car cons? not] [:body .car :body .cdr recur cons]
            [:body .car .car 'include eq?]
            [:body .car .cdr path-list-read-sexp-list
             :body .cdr recur
             list-append]
            else [:body .car :body .cdr recur cons]))
    #+end_src

*** path-list-read-sexp-list

    #+begin_src jojo
    (+jojo path-list-read-sexp-list
      {path-read-sexp-list} list-map
      null {list-append} list-foldr)
    #+end_src

*** path-read-sexp-list

    #+begin_src jojo
    (+jojo path-read-sexp-list (-> :path --)
      :path path->reading-path :real-path!
      (cond [:real-path path-open-read]
            [:file! :file file-input-stack
             input-stack-read-sexp-list
             :file file-close]
            else [:error-number!
                  "- path-read-sexp-list fail" w nl
                  "  path : " w :path w nl
                  "  real-path : " w :real-path w nl
                  "  " w :error-number error-number-print nl
                  debug]))
    #+end_src

*** list-car-filter

    #+begin_src jojo
    (+jojo list-car-filter (-> list :pred -- list)
      {:x! (if :x cons? not then false
               else :x .car :pred apply)}
      list-filter)
    #+end_src

*** module/generate-define-table

    #+begin_src jojo
    (+jojo module/generate-define-table
      (-> :list :name -- table)
      :list
      {'(+jojo +gene +disp) swap list-member?}
      list-car-filter {.cdr .car} list-map
      {(-> :s :table -- table)
       :table :s
       :name ': jo-append :s jo-append
       cons-table}
      null-table swap list-foldr)
    #+end_src

*** module/generate-export-table

    #+begin_src jojo
    (+jojo module/generate-export-table
      (-> :list :name -- table)
      :list
      {'export eq?}
      list-car-filter {.cdr} list-map
      null {list-append} list-foldr
      {(-> :s :table -- table)
       :table :s
       :name ': jo-append :s jo-append
       cons-table}
      null-table swap list-foldr)
    #+end_src

*** module/export-check

    #+begin_src jojo
    (+jojo module/export-check
      (-> :define-table :export-table --)
      (if :define-table :export-table table-contain? then end)
      "- module/export-check fail" w nl
      "  define-table does not contain export-table" w nl
      "  define-table : " w :define-table w nl
      "  export-table : " w :export-table w nl
      debug)
    #+end_src

*** module/collect-import-table-list

    #+begin_src jojo
    (+jojo module/collect-import-table-list
      {'import eq?} list-car-filter
      {.cdr module/import-one} list-map)
    #+end_src

*** module/import-one

    #+begin_src jojo
    (+jojo module/import-one (-> :body -- table)
      :body .car :name!
      (if :name module-loaded? not then
          :name module-load)
      :body sexp-list-run :result!
      (if :result table? then :result end)
      "- module/import-one fail" w nl
      "  result of body is not table" w nl
      "  body : " w :body w nl
      "  result : " w :result w nl
      debug)
    #+end_src

*** sexp-for-module?

    #+begin_src jojo
    (+jojo sexp-for-module? (-> :sexp -- <bool>)
      '(import export)
      {:jo! (if :sexp cons? not then false
                else :jo :sexp .car eq?)}
      list-any?)
    #+end_src

*** sexp-table-substitute

    #+begin_src jojo
    (+jojo sexp-table-substitute (-> :sexp :table -- sexp)
      (cond [:sexp cons?]
            [:sexp .car :table recur
             :sexp .cdr {:table sexp-table-substitute} list-map
             cons]
            else (if :table :sexp table-find then end
                     else :sexp)))
    #+end_src

*** module-loaded?

    #+begin_src jojo
    (+jojo module-loaded? (-> :name -- <bool>)
      (if :name jo-bound?
          then :name sexp-run table?
          else false))
    #+end_src

*** module-find-path

    #+begin_src jojo
    (+jojo module-find-path-from-dir
      (-> :dir :name -- path true or false)
      :dir "/modules/" string-append
      :name jo->string string-append
      "/module.jo" string-append :path!
      (if :path path-readable?
          then :path true
          else false))

    (+jojo module-find-path
      (-> :name -- path true or false)
      (cond [current-running-dir :name module-find-path-from-dir] true
            [usr-jojo-dir :name module-find-path-from-dir] true
            [sys-jojo-dir :name module-find-path-from-dir] true
            else false))
    #+end_src

*** module-load

    #+begin_src jojo
    (+jojo module-load (-> :name --)
      (if :name module-find-path
          then path-load
          else
          "- module-load fail" w nl
          "  can not find module : " w :name w nl
          "  current-reading-dir : " w current-reading-dir w nl
          "  usr-jojo-dir : " w usr-jojo-dir w nl
          "  sys-jojo-dir : " w sys-jojo-dir w nl
          debug))
    #+end_src

*** (run)

    #+begin_src jojo
    (+jojo run (-> :body --)
      :body expend-include :body!

      :body module/collect-import-table-list
      :import-table-list!

      :import-table-list
      {(if table-merge not then
           "- (run) fail to merge import-table-list" w nl
           "  import-table-list : " w :import-table-list w nl
           "  key : " w w nl
           debug)}
      null-table swap list-foldr :import-table!

      :import-table :rename-table!

      :body
      {sexp-for-module? not} list-filter
      {:rename-table sexp-table-substitute} list-map
      sexp-list-run)
    #+end_src

*** [note] table processing in module system

    - we can use table processing functions
      to handle the name unique-name binding

      #+begin_src jojo
      (note take r7rs import declaration for example
        (import <import-set>)
        where <import-set>
        = <library-name>
        | (only <import-set> <identifier> ...)
        | (except <import-set> <identifier> ...)
        | (prefix <import-set> <identifier>)
        | (rename <import-set> (<old-identifier> <new-identifier>) ...) )

      (note in jojo we can use table processing functions like :
        '(<name> ...) only
        '(<name> ...) except
        <prefix> prefix
        '(table <old> <new>, ...) rename)

      (note but these names are too general
        for table processing functions
        we use the following instead :
        only   : {<name> eq?} table-key-filter
        except : {<name> eq? not} table-key-filter
        prefix : {<prefix> swap jo-append} table-key-map
        rename : {(if dup <old> eq? then drop <new> else)} table-key-map)

      (note we can abstract those special use of
        table processing functions :
        '(<name> ...) import-only
        '(<name> ...) import-except
        <prefix> import-prefix
        '(table <old> <new>, ...) import-rename)
      #+end_src

*** import-only

    #+begin_src jojo
    (+jojo import-only (-> table :list -- table)
      {(-> :name -- <bool>)
       :list {:name eq?} list-any?}
      table-key-filter)
    #+end_src

*** import-except

    #+begin_src jojo
    (+jojo import-except (-> table :list -- table)
      {(-> :name -- <bool>)
       :list {:name eq? not} list-any?}
      table-key-filter)
    #+end_src

*** import-prefix

    #+begin_src jojo
    (+jojo import-prefix (-> table :prefix-jo -- table)
      {:prefix-jo swap jo-append}
      table-key-map)
    #+end_src

*** import-rename

    #+begin_src jojo
    (+jojo import-rename (-> table :renaming -- table)
      {:renaming sexp-table-substitute}
      table-key-map)
    #+end_src

*** [test]

    #+begin_src jojo
    (test

      (+module hi/0.0.0
        (export
          hi1 hi3 hi5)
        (+jojo hi1 "hi1")
        (+jojo hi2 "hi2")
        (+jojo hi3 "hi")
        (+jojo hi4 "hi4")
        (+jojo hi5 "hi5"))

      (+module ya/0.0.0
        (export
          ya1 ya3 ya5)
        (+jojo ya1 "ya1")
        (+jojo ya2 "ya2")
        (+jojo ya3 "ya")
        (+jojo ya4 "ya4")
        (+jojo ya5 "ya5"))

      (+module hiya/0.0.0
        (import hi/0.0.0
          '(hi3) import-only
          'hi- import-prefix
          (table 'hi-hi3 'hi) import-rename)
        (import ya/0.0.0
          '(ya3) import-only
          (table 'ya3 'ya) import-rename)
        (export
          hiya)
        (+jojo hiya hi ya string-append))

      (assert
        hiya/0.0.0:hiya "hiya" equal?)

      (assert
        (run (import hiya/0.0.0)
          hiya "hiya" equal?)))
    #+end_src

* the-story-begin

*** welcome-to-jojo

    #+begin_src jojo
    (+jojo welcome-to-jojo
      "welcome to jojo's programming adventure ^-^/" w nl)
    #+end_src

*** the-story-begin

    #+begin_src jojo
    (+jojo the-story-begin
      (cond
        [cmd-number 1 eq?]
        [repl-flag-on
         welcome-to-jojo
         print-data-stack
         terminal-input-stack repl]

        else
        [core-flag-off
         test-flag-off
         repl-flag-off
         cmd-list .cdr .car path-load]))

    the-story-begin
    #+end_src
