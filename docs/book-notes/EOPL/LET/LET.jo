(note program = exp)
(note exp
  = <const-exp>
  | <zero?-exp>
  | <if-exp>
  | <diff-exp>
  | <var-exp>
  | <let-exp>)

(note expval
  = <num-val>
  | <bool-val>)

(+data <num-val> .num)
(+data <bool-val> .bool)

(note env = <null-env> | <cons-env>)

(+data <null-env>)
(+data <cons-env> .rest .var .val)

(+jojo env-apply (-> :var :env -- expval)
  (case :env
    <null-env> ["- env-apply fail" w nl
                "  can not find var : " w :var w nl
                debug]
    <cons-env> (if :env .var :var equal?
                   then :env .val
                   else :var :env .rest recur)))

(+data <const-exp> .num)
(+data <zero?-exp> .exp1)
(+data <if-exp>    .exp1 .exp2 .exp3)
(+data <diff-exp>  .exp1 .exp2)
(+data <var-exp>   .var)
(+data <let-exp>   .var .exp1 .body)

(note
  (= :n const-exp :env eval, :n num-val)
  (= :var var-exp :env eval, :var :env env-apply)
  (= :exp1 :exp2 diff-exp :env eval,
     :exp1 :env eval .num
     :exp2 :env eval .num sub num-val))

(+jojo init-env
  null-env
  ':x 10 num-val cons-env
  ':v 5  num-val cons-env
  ':i 1  num-val cons-env)

(+jojo program-eval (-> :program -- expval)
  :program init-env eval)

(note
  (= :exp1 :env eval, :val1)
  ------------------------------------
  (= :exp1 zero?-exp :env eval
     (if :val1 .num 0 eq?
       then true  bool-val
       else false bool-val)))

(note
  (= :exp1 :env eval, :val1)
  ------------------------------------
  (= :exp1 :exp2 :exp3 if-exp :env eval
     (if :val1 .bool
       then :exp2 :env eval
       else :exp3 :env eval)))

(note
  (= :exp1 :exp2 :exp3 if-exp :env eval
     (if :exp1 :env eval .bool
       then :exp2 :env eval
       else :exp3 :env eval)))

(note

  null-env ':x 33 cons-env ':y 22 cons-env :env!

  (=
   '(if (zero? (diff :x 11))
      (diff :y 2)
      (diff :y 4))
   parse :env eval

   (if '(zero? (diff :x 11)) parse :env eval .bool
     then '(diff :y 2) parse :env eval
     else '(diff :y 4) parse :env eval)

   (if false bool-val .bool
     then '(diff :y 2) parse :env eval
     else '(diff :y 4) parse :env eval)

   (if false
     then '(diff :y 2) parse :env eval
     else '(diff :y 4) parse :env eval)

   '(diff :y 4) parse :env eval

   18 num-val))

(note
  (= :exp1 :env eval, :val1)
  ------------------------------------
  (= :var :exp1 :body :env eval,
     :body :env :var :val1 cons-env eval))

(note
  (= :var :exp1 :body :env eval,
   :env, :var, :exp1 :env eval, cons-env
   :body swap eval))

(+jojo eval (-> :exp :env -- expval)
  (case :exp
    <const-exp> [:exp .num  num-val]
    <zero?-exp> [:exp .exp1 :env recur .num 0 equal? bool-val]
    <if-exp>    (if :exp .exp1 :env recur .bool
                    then :exp .exp2 :env recur
                    else :exp .exp3 :env recur)
    <diff-exp>  [:exp .exp1 :env recur .num
                 :exp .exp2 :env recur .num sub num-val]
    <var-exp>   [:exp .var :env env-apply]
    <let-exp>   [:env, :exp .var, :exp .exp1 :env recur, cons-env
                 :exp .body swap recur]))

(note
  123 -- <const-exp>
  (diff ... ...) -- <diff-exp>
  (if ... ... ...) -- <if-exp>
  (zero? ...) -- <zero?-exp>
  :var -- <var-exp>
  (let :var ... ...) -- <let-exp>)

(+jojo parse (-> :sexp -- exp)
  (cond
    [:sexp int-jo?]   [:sexp jo->int const-exp]
    [:sexp local-jo?] [:sexp var-exp]
    [:sexp cons? not] ["- parse fail" w nl
                       "  can not parse sexp : " w
                       :sexp sexp-write nl
                       debug]
    else [:sexp parse/cons]))

(+jojo parse/cons (-> :sexp -- exp)
  :sexp .car :key!
  :sexp .cdr :body!
  (cond [:key 'zero? eq?] [:body parse/spread zero?-exp]
        [:key 'if eq?]    [:body parse/spread if-exp]
        [:key 'diff eq?]  [:body parse/spread diff-exp]
        [:key 'let eq?]   [:body .car :body
                           .cdr parse/spread let-exp]))

(+jojo parse/spread {parse} list-map list-spread)

(+jojo LET (-> :body --)
  :body {parse program-eval expval-write nl} list-for-each)

(+jojo expval-write (-> :expval)
  (case :expval
    <num-val>  [:expval .num w]
    <bool-val> [:expval .bool w]))
